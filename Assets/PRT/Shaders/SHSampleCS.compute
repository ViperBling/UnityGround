#pragma kernel MainCS

#include "Packages/com.unity.render-pipelines.universal/ShaderLibrary/Core.hlsl"
#include "SH.hlsl"

RWStructuredBuffer<Surfel> _Surfels;
TextureCube _WorldPosCube;
TextureCube _NormalCube;
TextureCube _AlbedoCube;
SamplerState sampler_point_clamp;
float _RandSeed;
float4 _ProbePos;

// ref: https://stackoverflow.com/questions/4200224/random-noise-functions-for-glsl
float rand(float2 uv)
{
    return frac(sin(dot(uv, float2(12.9898, 78.233))) * 43758.5453);
}

// ref: Unreal Engine 4, MonteCarlo.ush
float3 UniformSphereSample(float u, float v)
{
    const float C_PI = 3.14159265359f;
    float phi = degrees(2.0 * C_PI * u);
    float cosine_theta = 1.0 - 2.0 * v;
    float sine_theta = sqrt(1.0 - cosine_theta * cosine_theta);
    
    float x = sine_theta * cos(phi);
    float y = sine_theta * sin(phi);
    float z = cosine_theta;

    return float3(x, y, z);
}

[numthreads(32, 16, 1)]
void MainCS(uint3 id : SV_DispatchThreadID)
{
    float2 xy = float2(id.x, id.y) / float2(32, 16);
    xy += float2(2, 1) * _RandSeed;

    float u = rand(xy * 1.0);
    float v = rand(xy * 2.0);
    float3 dir = UniformSphereSample(u, v);

    Surfel surfel;

    surfel.position = _WorldPosCube.SampleLevel(sampler_point_clamp, dir, 0).rgb;
    surfel.albedo = _AlbedoCube.SampleLevel(sampler_point_clamp, dir, 0).rgb;

    float4 normalAndMask = _NormalCube.SampleLevel(sampler_point_clamp, dir, 0);
    surfel.normal = normalAndMask.xyz;
    surfel.skyMask = saturate(1.0 - normalAndMask.w);

    surfel.position += (_ProbePos.xyz + dir) * surfel.skyMask;

    uint surfelIndex = id.x * 16 + id.y;
    _Surfels[surfelIndex] = surfel;
}